plugins {
    id 'org.jetbrains.intellij' version '0.4.9' apply false
}

group = 'de.lukweb.share'
version '1.0-SNAPSHOT'

// The default configuration for every submodule
subprojects {

    apply plugin: 'java'
    group = 'de.lukweb.share'
    
    repositories {
        mavenCentral()
    }
    
    sourceCompatibility = 1.8
    
    dependencies {
        testImplementation group: 'junit', name: 'junit', version: '4.12'
    }
    
}

// All of the default configuration for the Intellij plugins
configure(subprojects.findAll { it.name == 'HasteIt' || it.name == 'DiscordBeam'}) {

    apply plugin: 'org.jetbrains.intellij'

    dependencies {
        implementation project(':ShareBase')
    }

    intellij {
        version '2018.3.3' // Full list of IntelliJ IDEA releases: https://www.jetbrains.com/intellij-repository/releases
        updateSinceUntilBuild false // Disables updating until-build attribute in plugin.xml
    }

    publishPlugin {
        // Publishing with gradle.properties
        username project.ext.properties.publishUsername
        token project.ext.properties.publishToken
    }
    
}